SetCellSize(10e-9, 10e-9, 100e-9) // 10nm3 resolution, 100nm for z-layer (single-layer)
SetGridSize(500, 100, 1) // 5um x 1um x 100 nm => 1um for design region, 2um for waveguides
B_ext = Vector(0, 0, 200e-3) // 200mT magnetisation
Msat = 0 // default: no magnetisation saturation
Aex = 3.5e-12 // J/m
alpha = 2e-4 // LL damping constant

// ================== MULTIPLEXER ==================

input_waveguide := Cuboid(2e-6, 300e-9, 100e-9).Transl(-1.5e-6, 0, 0)
design_region := Cuboid(1e-6, 1e-6, 100e-9).Transl(0, 0, 0)
output_waveguide_1 := Cuboid(2e-6, 300e-9, 100e-9).Transl(1.5e-6, 350e-9, 0)
output_waveguide_2 := Cuboid(2e-6, 300e-9, 100e-9).Transl(1.5e-6, -350e-9, 0)

DefRegion(1, input_waveguide) // input waveguide
DefRegion(2, design_region) // design region
DefRegion(3, output_waveguide_1) // output waveguide 1 (top)
DefRegion(4, output_waveguide_2) // output waveguide 2 (bottom)

multiplexer := design_region.Add(input_waveguide).Add(output_waveguide_1).Add(output_waveguide_2)

SetGeom(multiplexer)

Msat.SetRegion(1, 140e3)
Msat.SetRegion(2, 140e3)
Msat.SetRegion(3, 140e3)
Msat.SetRegion(4, 140e3)

// ================= EDGE DAMPENERS =================

DefRegion(5, xrange(-2.5e-6, -2.4e-6)) // input waveguide dampener
DefRegion(6, xrange(2.4e-6, 2.5e-6)) // output waveguide dampener

alpha.SetRegion(5, 0.5)
alpha.SetRegion(6, 0.5)

// ================ OUTPUT DETECTORS ================

// DefRegion(7, Cuboid(0.1e-6, 300e-9, 100e-9).Transl(1.95e-6, 350e-9, 0)) // top waveguide detection zone
// DefRegion(8, Cuboid(0.1e-6, 300e-9, 100e-9).Transl(1.95e-6, -350e-9, 0)) // bottom waveguide detection zone

// ============= RUNNING THE SIMULATION =============

Relax()

Save(m)
AutoSave(m, 50e-12)

// Excitation region
DefRegion(9, xrange(-2.0e-6, -1.9e-6))
B_ext.SetRegion(9, Vector(0, 0.1e-3 * (sin(2 * pi * 2.6e9 * t) + sin(2 * pi * 2.8e9 * t)), 200e-3)) // excitation: 0.1 mT @ 2.6, 2.8 GHz in y-direction, 200mT in z-direction

Run(120e-9)